package Pixhawk_Software
public
  with Pixhawk_Types;

  process STM32F100_Firmware
  features
    Serial_Packet_TX: out data port Pixhawk_Types::Serial_Packet;
    Serial_Packet_RX: in data port Pixhawk_Types::Serial_Packet;
    telemetry_Packet_RX: in data port Pixhawk_Types::Serial_Packet;
    
  flows
    f1 : flow path telemetry_Packet_RX -> Serial_Packet_TX { latency => 1 ms .. 2 ms; };
    f2 : flow source Serial_Packet_TX { latency => 1 ms .. 2 ms; };

  end STM32F100_Firmware;


  process STM32F765_Firmware --main FMU
  features
    Serial_Packet_Rx : in data port Pixhawk_Types::Serial_Packet;
    Serial_Packet_Tx : out data port Pixhawk_Types::Serial_Packet;
    DOFs_Motion_Sensor : in data port Pixhawk_Types::Six_Axis.impl;
    DOFs_IMU : in data port Pixhawk_Types::Six_Axis.impl;
    pressure : in data port;
    gps_reading : in data port;
    compass_reading : in data port;
    

    Rate_1         : out data port Pixhawk_Types::PWM_Rate;
    Rate_2         : out data port Pixhawk_Types::PWM_Rate;
    Rate_3         : out data port Pixhawk_Types::PWM_Rate;
    Rate_4         : out data port Pixhawk_Types::PWM_Rate;

  flows
    f1 : flow path Serial_Packet_Rx     -> Rate_1;
    f2 : flow path DOFs_IMU             -> Rate_1; 
  
  end STM32F765_Firmware;

  process implementation STM32F765_Firmware.impl --internals of FMU 
  subcomponents
    MAVLink_Tx_Task     : thread MAVLink_Tx_Task;
    MAVLink_Rx_Task     : thread MAVLink_Rx_Task;
    Power_Management : thread Power_Management;
    Main_Loop        : thread Main_Loop;

  connections
    SC1 : port MAVLink_Tx_Task.Msg -> Serial_Packet_Tx;
    SC8 : port Main_Loop.Serial_Packet_Tx -> MAVLink_Tx_Task.Serial_Packet;

    SC2 : port Serial_Packet_Rx -> MAVLink_Rx_Task.Msg;
    SC7 : port MAVLink_Rx_Task.Serial_Packet -> Main_Loop.Serial_Packet_Rx;

    SC3 : port Main_Loop.Rate_1 -> Rate_1;
    SC4 : port Main_Loop.Rate_2 -> Rate_2;
    SC5 : port Main_Loop.Rate_3 -> Rate_3;
    SC6 : port Main_Loop.Rate_4 -> Rate_4;

    SC9 : port DOFs_Motion_Sensor -> Main_Loop.DOFs_motion;
    SC10 : port DOFs_IMU -> Main_Loop.DOFs_imu;
    SC11 : port pressure -> Main_Loop.pressure;
    SC12 : port gps_reading -> Main_Loop.gps_reading;
    SC13 : port compass_reading -> Main_Loop.compass_reading;
    
  flows
    f1 : flow path Serial_Packet_Rx -> SC2 -> MAVLink_Rx_Task.fs1 -> SC7
      -> Main_Loop.fs1 -> SC3 -> Rate_1;
    --f2 : flow path DOFs -> C9 -> Main_Loop.fs2 -> C3 -> Rate_1; --Which DOFs should this be??
  end STM32F765_Firmware.impl;


---MAVLINK protocols
-- https://arxiv.org/pdf/1906.10641.pdf
  thread MAVLink_Tx_Task
  features
    Msg : out data port Pixhawk_Types::Serial_Packet;
    Serial_Packet : in data port Pixhawk_Types::Serial_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us; 
  end MAVLink_Tx_Task;


  thread MAVLink_Rx_Task
  features
    Msg : in data port Pixhawk_Types::Serial_Packet;
    Serial_Packet : out data port Pixhawk_Types::Serial_Packet;
  flows
    fs1 : flow path Msg -> Serial_Packet;
  properties
    Priority => 2;
    Dispatch_Protocol => Sporadic;
    Period => 1000 us;
    Compute_Execution_Time => 10 us .. 50 us;
  end MAVLink_Rx_Task;


  thread Power_Management
  properties
    Priority => 2;
    Dispatch_Protocol => Periodic;
    Period => 500 us;
    Compute_Execution_Time => 10 us .. 20 us;
  end Power_Management;


  thread Main_Loop
  features
    DOFs_motion              : in data port Pixhawk_Types::Six_Axis.impl;
    DOFs_imu                 : in data port Pixhawk_Types::Six_Axis.impl;
    pressure                 : in data port;
    gps_reading				 : in data port;
    compass_reading			 : in data port;

    Serial_Packet_Rx : in data port Pixhawk_Types::Serial_Packet;
    Serial_Packet_Tx : out data port Pixhawk_Types::Serial_Packet;

    Rate_1            : out data port Pixhawk_Types::PWM_Rate;
    Rate_2            : out data port Pixhawk_Types::PWM_Rate;
    Rate_3            : out data port Pixhawk_Types::PWM_Rate;
    Rate_4            : out data port Pixhawk_Types::PWM_Rate;

  flows
    fs1 : flow path Serial_Packet_Rx -> Rate_1;
    fs2 : flow path DOFs_imu         -> Rate_1;

  properties
    Priority => 3;
    Dispatch_Protocol => Periodic;
    Period => 2000 us;
    Compute_Execution_Time => 100 us .. 200 us;
  end Main_Loop;

end Pixhawk_Software;