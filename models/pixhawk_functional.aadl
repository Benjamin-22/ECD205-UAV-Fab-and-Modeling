package Pixhawk_functional

-- This package models the abstract functional chain of the Pixhawk,
-- from https://wiki.bitcraze.io/doc:Pixhawk:dev:fimware:sensor_to_control

public

  abstract Accelero
  features
    Accelero_Out : out data port;

  flows
    f1 : flow source Accelero_Out {latency => 100 us .. 300 us; };		

  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Accelero;


  abstract Gyro
  features
    Gyro_Out : out data port;
    
  flows
    f1 : flow source Gyro_Out {latency => 100 us .. 300 us; };		
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Gyro;


  abstract Magneto
  features
    Magneto_Out : out data port;
    
 flows
   f1 : flow source Magneto_Out {latency => 100 us .. 300 us; };		
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Magneto;
  
  
  abstract Pressure
  features
    Pressure_Out : out data port;
    
 flows
   f1 : flow source Pressure_Out {latency => 100 us .. 300 us; };		
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Pressure;


  abstract Video
  features
   Video_Out : out data port;

  flows
    f1 : flow source Video_Out {latency => 1800 us .. 2200 us; };

  properties
	Dispatch_Protocol => Periodic;
	Period => 17 ms; -- 60 Hz
  end Video;


  abstract Sensor_Fusion
  features
    Accelero_In : in data port;
    Gyro_In     : in data port;
    Magneto_In  : in data port;
    Pressure_In : in data port;
    Data_F_Out  : out data port; -- Fusioned data
    
  flows
    f1 : flow path Accelero_In -> Data_F_Out {latency => 200 us .. 400 us; };	
    f2 : flow path Gyro_In -> Data_F_Out {latency => 200 us .. 400 us; };
    f3 : flow path Magneto_In -> Data_F_Out {latency => 200 us .. 400 us; };
    f4 : flow path Pressure_In -> Data_F_Out {latency => 200 us .. 400 us; };  
    --Video?
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 4 ms; --  250 Hz
  end Sensor_Fusion;


  abstract Controller
  features
    Data_F_In : in data port;
    Gyro_In   : in data port;
    
    Motor_Out : out data port;
    
    Video_In : in data port;
    TC : in data port;
  flows
  	f1 : flow path Data_F_In -> Motor_Out {latency => 200 us .. 400 us; };	
    f2 : flow path Gyro_In -> Motor_Out {latency => 200 us .. 400 us; };
    f3 : flow sink Video_In {latency => 100 us .. 500 us; };
    f4 : flow sink TC {latency => 100 us .. 500 us; };
    --Should TC be here as a flow sink or flow path??	
    
  properties
    Dispatch_Protocol => Periodic;
    Period => 2 ms; --  500 Hz
  end Controller;


  abstract Motors
  features
    Motor_In : in data port;
    
  flows
    f1 : flow sink Motor_In {latency => 100 us .. 500 us; };
    			
  properties
    --Dispatch_Protocol => Aperiodic;
    Dispatch_Protocol => Periodic;
    Period => 2ms;
  end Motors;


  abstract Pilot
  features
	Order : out data port;
	
	-- Is below here correct
	flows
    f1 : flow source Order {latency => 1800 us .. 2200 us; };

  properties
	Dispatch_Protocol => Sporadic;
	Period => 17 ms; 
	
  end Pilot;


  abstract Telemetry_rec
  features
	Telemetry_In : in data port;
	Telemetry_Out : out data port;

  flows
	--tx : flow source Telemetry_Out ;
	rx : flow path Telemetry_In -> Telemetry_Out {latency => 100 us .. 300 us;}; -- Should this be a flow sink or flow path like in Sensor_Fusion?

  properties
	Dispatch_Protocol => Sporadic;
  end Telemetry_rec;

  
  system Pixhawk_Functional_Chain
  end Pixhawk_Functional_Chain;

  system implementation Pixhawk_Functional_Chain.impl
  subcomponents
    --  Sensing
    Acc        : abstract Accelero;
    Gyro       : abstract Gyro;
    Magneto    : abstract Magneto;
    Pressure   : abstract Pressure;

	Video 	   : abstract Video;
	
    Pilot      : abstract Pilot;
    Telemetry_rec : abstract Telemetry_rec;

    --  Computing
    Fusion     : abstract Sensor_Fusion;
    Controller : abstract Controller;
    
    --  Acting
    Motors : abstract Motors;

  connections
    C1 : port Acc.Accelero_Out     -> Fusion.Accelero_In;
    C2 : port Gyro.Gyro_Out        -> Fusion.Gyro_In;
    C3 : port Magneto.Magneto_Out  -> Fusion.Magneto_In;
    C4 : port Pressure.Pressure_Out  -> Fusion.Pressure_In;

    C5 : port Gyro.Gyro_Out        -> Controller.Gyro_In;
    C6 : port Fusion.Data_F_Out    -> Controller.Data_F_In;

    C7 : port Controller.Motor_Out -> Motors.Motor_In;

    C8 : port Pilot.Order -> Telemetry_rec.Telemetry_In;
    C9 : port Telemetry_rec.Telemetry_Out -> Controller.TC;
    c10 : port Video.Video_out -> Controller.Video_In;
    

  flows
  	f_etef1 : end to end flow Gyro.f1 -> C2 -> Fusion.f2 -> C6 -> Controller.f1 -> C7 -> Motors.f1 { latency => 0 ms .. 10 ms;};
  	f_etef1bis : end to end flow Gyro.f1 -> C5 -> Controller.f2 -> C7 -> Motors.f1 { latency => 0 ms .. 10 ms;};
    f_etef2 : end to end flow Acc.f1 -> C1 -> Fusion.f1 -> C6 -> Controller.f1 -> C7 -> Motors.f1 { latency => 0 ms .. 10 ms;};
    
    f_etef3 : end to end flow Video.f1 -> c10 -> Controller.f3 { latency => 0 ms .. 10 ms;};
    f_etef4 : end to end flow Pilot.f1 -> c8 -> Telemetry_rec.rx -> c9 -> Controller.f4 { latency => 0 ms .. 10 ms;};
  end Pixhawk_Functional_Chain.impl;

end Pixhawk_functional;